plugins {
    id 'java'
    id 'eclipse' //both are required for the ideCopyResourcesTask
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.1.2'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

base {
    archivesName = "${mod_id}-forge-${minecraft_version}"
}
version = project.mod_version
group = project.maven_group

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.20'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true
    runs {
        client {
            workingDirectory project.file('runs/' + name)
            ideaModule("${rootProject.name}.${project.name}.main")
            singleInstance true
            taskName "${mod_id}-forge4.0-Client"

            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'

            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            args "-mixin.config=${mod_id}.mixins.json"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                    source project(":core").sourceSets.main
                }
            }
        }

        clientAlt {
            parent minecraft.runs.client
            workingDirectory project.file('runs/' + name)
            taskName "${mod_id}-forge4.0-Client-2"
            args '--username', 'Alt'
        }

        server {
            workingDirectory project.file('runs/' + name)
            ideaModule("${rootProject.name}.${project.name}.main")
            singleInstance true
            taskName "${mod_id}-forge4.0-Server"

            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            args "-mixin.config=${mod_id}.mixins.json"

            mods {
                "${mod_id}" {
                    source project(":core").sourceSets.main
                    source sourceSets.main
                }
            }
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

repositories {
    maven { url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":core")
    minecraftLibrary(group: 'com.eliotlash.mclib', name: 'mclib', version: "20")
    jarJar(group: 'com.eliotlash.mclib', name: 'mclib', version: "[20,21)")
    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    }
}

jarJar.enable()

//Makes the jarJar artifact the default one.
tasks.jarJar.configure {
    it.archiveClassifier.set("")
    finalizedBy("reobfJarJar")
}

tasks.jar.configure {
    it.archiveClassifier.set("slim")
    //it.finalizedBy("reobfJar")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
    source(project(":core").sourceSets.main.allSource)
}

sourcesJar {
    from project(":core").sourceSets.main.allSource
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Specification-Title"     : "${mod_display_name}",
                "Specification-Vendor"    : "Gecko",
                "Specification-Version"   : "4",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Gecko",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

mixin { // Add mixins refmap
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version_range: minecraft_version_range,
        forge_version_range: forge_version_range,
        loader_version_range: forge_loader_range,
        mod_id: mod_id,
        mod_name: mod_display_name,
        mod_license: mod_license,
        mod_version: mod_version,
        mod_authors: mod_authors,
        mod_description: mod_description
]

processResources {
    from project(":core").sourceSets.main.resources

    inputs.properties replaceProperties

    filesMatching(resourceTargets) {
        expand replaceProperties
    }

    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            //Excludes the non jij'd jar from being published at all.
            artifacts = [sourcesJar, javadocJar]
            jarJar.component(it)
            artifactId base.archivesName.get()
            //We don't want users to have mclib as a dep when we JiJ for functionality.
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if("mclib".equals(dep.artifactId.last().value().last())) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }
    repositories {
        if (System.getenv("cloudUsername") == null && findProperty("cloudUsername") == null) {
            mavenLocal()
        }
        else maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            credentials {
                def envUsername = System.getenv("cloudUsername")
                def envPassword = System.getenv("cloudPassword")
                username = envUsername == null ? findProperty("cloudUsername") : envUsername
                password = envPassword == null ? findProperty("cloudPassword") : envPassword
            }
        }
    }
}

if (file('key.properties').exists()) {
    curseforge {
        def curseProp = new Properties()
        File secretPropsFile = file("key.properties")
        curseProp.load(secretPropsFile.newInputStream())

        project {
            apiKey = curseProp.getProperty('curseKey')
            id = "388172"
            releaseType = "release"
            changelogType = 'text'
            mainArtifact = "build/libs/${mod_id}-forge-${minecraft_version}-${mod_version}"
            changelog = file('changelog.txt')
        	addGameVersion("1.20")
            addGameVersion("Forge")
            addGameVersion("Java 17")
        }
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = '8BmcQJ2H' 
		versionNumber = project.mod_version 
		versionName = 'Forge 1.20'
		uploadFile = jarJar
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.20']
		loaders = ['forge']
	}
}
