plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'java'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base {
    archivesName = "geckolib-fabric-${minecraft_version}"
}

version = mod_version
group = maven_group

repositories {
    mavenCentral()
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup "com.eliotlash.mclib"
        }
    }
}

dependencies {
    // Minecraft/Fabric deps
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
	//mappings( loom.layered() {
	//	officialMojangMappings()
	//	parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.14@zip")
	//})
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    compileOnly project(':core')
    compileOnly(include("com.eliotlash.mclib:mclib:20"))
    modLocalRuntime("com.eliotlash.mclib:mclib:20") //Since we shade this, we don't want this to be a dep in publishing.
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Specification-Title"     : "GeckoLib",
                "Specification-Vendor"    : "Gecko",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : "Gecko",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

loom {
	accessWidenerPath = file("src/main/resources/geckolib.aw")
    runs {
        named("client") {
            client()
            configName = "Fabric Client"
            ideConfigGenerated(true)
            runDir("runs/"+name)
            programArg("--username=Dev")
        }
        named("server") {
            server()
            configName = "Fabric Server"
            ideConfigGenerated(true)
            runDir("runs/"+name)
        }
    }

}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
    source(project(":core").sourceSets.main.allSource)
}

java {
	withSourcesJar()
    withJavadocJar()
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

sourcesJar {
    from project(":core").sourceSets.main.allSource
}

def replaceProperties = [
        version: version,
        mod_id: mod_id,
        mod_display_name: mod_display_name,
        mod_license: mod_license,
        mod_authors: mod_authors,
        mod_description: mod_description,
        minecraft_version: minecraft_version,
        fabric_loader_version: fabric_loader_version,
        fabric_api_version: fabric_api_version
]

processResources {
    inputs.property "version", version

    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }

    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java

            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if("mclib".equals(dep.artifactId.last().value().last())) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }
    repositories {
        //Don't personally like envVariable + findProperty but this is not my thing lol
        if (System.getenv("cloudUsername") == null && findProperty("cloudUsername") == null) {
            mavenLocal()
        }
        else maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            credentials {
                def envUsername = System.getenv("cloudUsername")
                def envPassword = System.getenv("cloudPassword")
                username = envUsername == null ? findProperty("cloudUsername") : envUsername
                password = envPassword == null ? findProperty("cloudPassword") : envPassword
            }
        }
    }
}

if (file('key.properties').exists()) {
    curseforge {

    def curseProp = new Properties()
    File secretPropsFile = file("key.properties")
    curseProp.load(secretPropsFile.newInputStream())

    project {
        apiKey = curseProp.getProperty('curseKey')
        id = "388172"
        releaseType = "release"
        changelogType = 'text'
        changelog = file('changelog.txt')
		addGameVersion(minecraft_version)
        addGameVersion("Fabric")
        addGameVersion("Java 17")

        relations {
            requiredDependency 'fabric-api'      
        }
        mainArtifact(remapJar)

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
    	//debug = true
        forgeGradleIntegration = false
    }
   }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = '8BmcQJ2H' 
		versionNumber = mod_version
		versionName = "Fabric ${minecraft_version}"
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = [minecraft_version]
		versionType = "release"
		loaders = ['fabric']
		dependencies { 
        	required.project "fabric-api"
   		}
	}
}