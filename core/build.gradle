plugins {
    id 'java'
    id 'maven-publish'
}

archivesBaseName = "geckolib-core"

repositories {
    mavenCentral()
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup "com.eliotlash.mclib"
        }
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

sourcesJar {
    //Include the license into the sourcesJar
    from(rootProject.file("LICENSE"))
}

javadocJar {
    //Include the license into  the sourcesJar
    from(rootProject.file("LICENSE"))
}

dependencies {
    implementation "com.eliotlash.mclib:mclib:${mclib_version}"
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation "it.unimi.dsi:fastutil:${fastutil_version}"
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "${lang3_version}"
}

jar {
    from(rootProject.file("LICENSE"))
    manifest {
        attributes([
                'Specification-Title'     : project_name,
                'Specification-Vendor'    : authors,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project_name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        if (System.getenv("cloudUsername") == null  && findProperty("cloudUsername") == null) {
            mavenLocal()
        }
        else maven {
            name =  "cloudsmith"
            url = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            credentials {
                username = System.getenv("cloudUsername")
                password = System.getenv("cloudPassword")
            }
        }
    }
}